(def vm (require "vm"))
(def escodegen (require "escodegen"))
(def repl (require "repl"))
(def compiler (require "./compiler"))
(def reader (require "./reader"))

(defn make-session
  []
  (let* [command ""
         env (.create-env compiler)]
        (fn [string _ _ callback]
          ;; Append the string. Node wraps it in ()s, which is not a
          ;; Lisp-friendly thing to do...
          (aset command (+ command
                           (string.slice 1 -1)))
          (try
            (let* [tree (compiler.compile-string command env)
                   compiled (escodegen.generate tree)
                   result (vm.runInNewContext compiled env)]
                  (aset command "")
                  (callback null result))
            (catch e
                (if (= (.-type e) "UnbalancedForm")
                  (callback null undefined)
                  (do
                    (aset command "")
                    (callback null e))))))))

(defn start-repl
  []
  (repl.start {:prompt "=> "
               :eval (make-session)
               :terminal false
               :ignoreUndefined true
               :useColors true
               :useGlobal false}))
